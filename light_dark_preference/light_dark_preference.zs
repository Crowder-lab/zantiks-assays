# define experiment requirements
DEFINE acclimation_length 300  # 5 minutes
DEFINE bin_length 60           # 1 minute
DEFINE num_bins 30             # 30 minutes total experiment time

SET(TARGET_SIZE, 2)
SET(DETECTOR_THRESHOLD, 5)

SET(AUTOREF_MODE, MOVEMENT)
SET(AUTOREF_TIMEOUT, 30)

# no tracking marker
TARGETMARKER(0, 0, 0)

SET(THERMOSTAT,28)

LOAD(ARENAS, "a12.bmp")
LOAD(ZONES, "ldz12.bmp")

LOGFILE(2, "xy_position")
SET(LOG_STREAM_PERFRAME, 2)


ACTION MAIN

    SET(LOG_STREAM, 0)
    LOGCREATE("TEXT:TIME|TEXT:BIN_NUM|TEXT:ENDPOINT")
    LOGAPPEND("TEXT:A1_Z1|TEXT:A1_Z2")
    LOGAPPEND("TEXT:A2_Z1|TEXT:A2_Z2")
    LOGAPPEND("TEXT:A3_Z1|TEXT:A3_Z2")
    LOGAPPEND("TEXT:A4_Z1|TEXT:A4_Z2")
    LOGAPPEND("TEXT:A5_Z1|TEXT:A5_Z2")
    LOGAPPEND("TEXT:A6_Z1|TEXT:A6_Z2")
    LOGAPPEND("TEXT:A7_Z1|TEXT:A7_Z2")
    LOGAPPEND("TEXT:A8_Z1|TEXT:A8_Z2")
    LOGAPPEND("TEXT:A9_Z1|TEXT:A9_Z2")
    LOGAPPEND("TEXT:A10_Z1|TEXT:A10_Z2")
    LOGAPPEND("TEXT:A11_Z1|TEXT:A11_Z2")
    LOGAPPEND("TEXT:A12_Z1|TEXT:A12_Z2")
    LOGRUN()

    # Set up position tracking
    SET(LOG_STREAM, 2)
    LOGCREATE("TEXT:RUNTIME|TEXT:X_A1|TEXT:Y_A1|TEXT:X_A2|TEXT:Y_A2")
    LOGAPPEND("TEXT:X_A3|TEXT:Y_A3|TEXT:X_A4|TEXT:Y_A4")
    LOGAPPEND("TEXT:X_A5|TEXT:Y_A5|TEXT:X_A6|TEXT:Y_A6")
    LOGAPPEND("TEXT:X_A7|TEXT:Y_A7|TEXT:X_A8|TEXT:Y_A8")
    LOGAPPEND("TEXT:X_A9|TEXT:Y_A9|TEXT:X_A10|TEXT:Y_A10")
    LOGAPPEND("TEXT:X_A11|TEXT:Y_A11|TEXT:X_A12|TEXT:Y_A12")
    LOGRUN()
    LOGCREATE("RUNTIME|RAW_XY:A1-12")

    # set up for experiments
    SET(LOG_STREAM, 0)
    LIGHTS(ALL, OFF)
    AUTOREFERENCE()
    WAIT(acclimation_length)
    LIGHTS(LIGHT16, WHITE)
    SET(LOG_PERFRAME, ON)
    SET(COUNTER1, COUNTER_ZERO)
    VIDEO(99999999999, "light_dark_preference_tracking")

    INVOKE(TEST, num_bins)

    SET(LOG_PERFRAME, OFF)
    VIDEOSTOP()

COMPLETE


ACTION TEST

    SET(COUNTER1, COUNTER_INC)

    LOGDATA(DATA_SNAPSHOT, "BEGIN")

    WAIT(bin_length)

    LOGDATA(DATA_SNAPSHOT, "END")
    LOGDATA(DATA_SELECT, "BEGIN")
    LOGDATA(DATA_DELTA, "END")

    SET(LOG_STREAM, 0)
    LOGCREATE("RUNTIME|COUNTER1|TEXT:DISTANCE_IN_ZONE|ZONE_DISTANCES:A* Z1-2")
    LOGRUN()
    LOGCREATE("RUNTIME|COUNTER1|TEXT:TIME_SPENT_IN_ZONE|ZONE_TIMERS:A* Z1-2")
    LOGRUN()

COMPLETE

# vim: ft=zanscript
